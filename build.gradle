plugins {
	id 'java'
}

def pathToRisingWorld = 'D:/SteamLibrary/steamapps/common/RisingWorld/_New Version'
def pluginFolder = pathToRisingWorld + '/Plugins/AdvancedStatus'
def folder = new File(pluginFolder)

group 'paulevs.status'
version '0.3.1'

sourceCompatibility = JavaVersion.VERSION_20
targetCompatibility = JavaVersion.VERSION_20

repositories {
	mavenCentral()
	maven { url 'https://jitpack.io' }
}

configurations {
	implAndInclude
	implementation.extendsFrom(implAndInclude)
}

dependencies {
	implementation files(pathToRisingWorld + '/Data/SDK/PluginAPI.jar')
	implAndInclude 'com.github.paulevsGitch.UniLib:unilib-config-utils:cb9e6fb312'
}

jar {
	duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
	from configurations.implAndInclude.asFileTree.files.collect { zipTree(it) }
}

task zipFiles(type: Zip) {
	inputs.files(jar)
	from fileTree("build/libs")
	into rootProject.name
}

task complete {
	doLast {
		copy {
			from 'icons'
			into 'build/libs/icons'
		}
	}
}

task copyPlugin {
	doLast {
		copy {
			from jar
			into pluginFolder
		}
		copy {
			from 'icons'
			into pluginFolder + '/icons'
		}
	}
}

task cleanup(type: Delete) {
	delete fileTree('build/libs') {
		include '**/*.jar'
	}
}

if(folder.exists()) {
	zipFiles.finalizedBy copyPlugin
}

complete.finalizedBy(cleanup, zipFiles)
build.dependsOn zipFiles